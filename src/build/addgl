# ADDGL--
#
# addgl([table[],] uncode)
#
if ( $argc == 3 )
    g = SizeOf($1)
    k = 0
    while ( k < g )
        if ( $2 == $1[k] )
            break
        endif
        ++k
    endloop
    if ( $2 != $1[k] )
        Error( ToString(UCodePoint($2)) + " is not in the list!\n")
    endif
    $1[k] = 0
    shift
endif
Select(UCodePoint($1))

c = GlyphInfo("Color")
r = GlyphInfo("RefCount")
name = GlyphInfo("Name")

if ( r == 0 )
    Error("missing RefCount: " + name)
endif
if ( c == 0xffff )
    color = "B"
else
    if ( c == 0xff00 )
        color = "G"
    else
        color = "R"
    endif
endif

w = GlyphInfo("Width")
n = GlyphInfo("RefNames")
m = GlyphInfo("RefTransform")
u = ToString(UCodePoint(GlyphInfo("Unicode")))

x = 0;
while ( x < r )

    Select(n[x])
    z = ToString(UCodePoint(GlyphInfo("Unicode")))

    if ( m[x][0] == 1 && m[x][1] == 0 && m[x][2] == 0 && m[x][3] == 1 )

        if ( m[x][4] == 0 && m[x][5] == 0 )
            WriteStringToFile("glyph(" + u + "," + z + "," + ToString(w) + "," + color + ")\n", "glist", 1)
        else
            WriteStringToFile("glyph(" + u + "," + z + "," + ToString(w) + "," + color + "," + \
                ToString(m[x][4]) + "," + ToString(m[x][5]) + ")\n", "glist", 1)
        endif
    else
        s = ToString(m[x])
        WriteStringToFile("glyph(" + u + "," + z + "," + ToString(w) + "," + color + "," + \
            Strsub(s, 1, Strlen(s)-1) + ")\n", "glist", 1)
    endif
    ++x;
endloop
