# CAUDEXI.PE--
#
create()
if ( $argc == 2 )
    version = $1
else
    version = "2.0"
endif

source = ToString("../caudex-" + version + ".sfd")
target = ToString("../caudexi-" + version + ".sfd")

Print("Building " + target)
Open(source, 1)
SetMacStyle(0x02)
SetOS2Value("StyleMap", 0x01)
SetItalicAngle(-13)
SetFontNames("Caudex-Italic", "", "Caudex Italic", "Italic", "", version)
SetTTFName(0x409, 3, "FontForge : Caudex Italic : 12-5-2011")

Reencode("unicode")


unl_glyphs = [ \
    0u0398, \
    0u03D9, \
    0u0480, \
    0u049C, \
    0u049D, \
    0u04B8, \
    0u04B9, \
    0u04BC, \
    0u04BD, \
    0u04BE, \
    0u04BF, \
    0u04CB, \
    0u04CC, \
    0u211F, \
    0u2123, \
    0u2195, \
    0u21A8, \
    0uA73A, \
    0uA73B, \
    0uA74B, \
    0uA786 ]

i = 0
while ( i < SizeOf(unl_glyphs) )

    Select(unl_glyphs[i])
    SetCharColor(4)
    ++i;
endloop
ulink(4)


SelectAll()
Skew(13,0,0)
SelectGlyphsSplines()
RoundToInt()
AddExtrema()
RoundToInt()
Validate(1)
count = CharCnt()
i = 0
while ( i < count )

    Select(i)
    state = GlyphInfo("ValidationState")
    if ( state & 0x1 )
        if ( state & 0x8 )
            CorrectDirection()
            RoundToInt()
        endif
        if ( state & 0x4 )
            RemoveOverlap()
            RoundToInt()
        endif
        if ( state & 0x20 )
            AddExtrema(1)
            RoundToInt()
        endif
    endif
    ++i;
endloop
Select(0u00E6)
RoundToInt()
Select(0u0153)
RoundToInt()
RoundToInt()
Select(0u03BB)
RemoveOverlap()
RoundToInt()
Select(0u16B8)
RemoveOverlap()
RoundToInt()

Reencode("compacted")
Save(target)
Close()

Quit()
