# QSORT.PE--
#
# qsort(unicode[])
#
l = 0
h = SizeOf($1)-1
p = 0
a = Array(30)
b = Array(30)

while ( 1 )

    r = 0
    m = l + ((h - l + 1) / 2)

    x = $1[m]
    y = $1[l]
    $1[m] = y
    $1[l] = x

    i = l
    j = h + 1

    while ( 1 )

        i++
        while ( i <= h && $1[i] < $1[l] )
            i++
        endloop
        j--
        while ( j > l && $1[j] > $1[l] )
            j--
        endloop
        if ( j < i )
            break
        endif
        x = $1[i]
        y = $1[j]
        $1[i] = y
        $1[j] = x
    endloop

    x = $1[l]
    y = $1[j]
    $1[l] = y
    $1[j] = x

    if ( j - 1 - l >= h - i )
        if ( l + 1 < j )
            a[p] = l
            b[p] = j - 1
            p++
        endif

        if ( i < h )
            l = i
            r++
        endif
    else
        if ( i < h )
            a[p] = i
            b[p] = h
            p++
        endif
        if ( l + 1 < j )
            h = j - 1
            r++
        endif
    endif
    if ( r == 0 )
        p--
        if ( p >= 0 )
            l = a[p]
            h = b[p]
            r++
        else
            break
        endif
    endif
    if ( r == 0 )
        break
    endif
endloop
